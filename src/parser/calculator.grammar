%package "parser";
%class "Parser";
%import "ast.*";
%import "java.math.BigInteger";

%terminals            INT_LIT;
%terminals            PLUS, MINUS, TIMES, DIVIDE, MOD, EXP;
%terminals            LPAREN, RPAREN, SEMI, EQUALS;
%terminals            ID;

%typeof INT_LIT = "Integer";
%typeof Exp = "Exp";
%typeof ID = "String";
%typeof Program = "Program";
%typeof Assign = "Assign";

%goal Program;

%right EXP;
%left MOD, TIMES, DIVIDE;
%left PLUS, MINUS;

Exp     = Exp.e1 PLUS Exp.e2       {: return new Plus(e1, e2); :}
        | Exp.e1 MINUS Exp.e2      {: return new Minus(e1, e2); :}
        | Exp.e1 TIMES Exp.e2      {: return new Times(e1, e2); :}
        | Exp.e1 DIVIDE Exp.e2     {: return new Divide(e1, e2); :}
        | Exp.e1 MOD Exp.e2        {: return new Mod(e1, e2); :}
        | Exp.e1 EXP Exp.e2        {: return new Expn(e1, e2); :}
        | INT_LIT.i                {: return new IntLit(BigInteger.valueOf(i)); :}
        | LPAREN Exp.e RPAREN      {: return e; :}
        | ID.i                     {: return new Identifier(i); :}
;

Assign  = ID.i EQUALS Exp.e        {: return new Assign(i, e); :}
;

Program = Program.p Exp.e SEMI     {: p.add(e); return p; :}
        | Program.p Assign.a SEMI  {: p.add(a); return p; :}
        |                          {: return new Program(); :}
;